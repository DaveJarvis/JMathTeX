buildscript {
  repositories {
    flatDir dirs: 'lib'
  }
  dependencies {
    classpath ':proguard-gradle:'
    classpath ':proguard:'
  }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '6.0.0'
  id 'java'
}

repositories {
  mavenCentral()
  jcenter()

  maven {
    url "https://repo1.maven.org/maven2/"
  }

  flatDir {
    dirs 'libs'
  }
}

dependencies {
  implementation 'org.jdom:jdom2:2.0.6'
  implementation 'org.apache.commons:commons-math3:3.6.1'

  testImplementation 'junit:junit:4.13'
}

sourceCompatibility = targetCompatibility = 11

jar {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE

  manifest {
    attributes 'Main-Class': "com.whitemagicsoftware.tex.TeXFormulaTest"
  }

  from {
    (configurations.runtimeClasspath.findAll { !it.path.endsWith(".pom") }).collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }

  archiveFileName = "jmathtex.jar"

  exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

//noinspection UnnecessaryQualifiedReference
task squish(type: proguard.gradle.ProGuardTask) {
  libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
  libraryjars "${System.getProperty('java.home')}/jmods/java.desktop.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
  libraryjars "${System.getProperty('java.home')}/jmods/java.xml.jmod", jarfilter: '!**.jar', filter: '!module-info.class'

  injars 'build/libs/jmathtex.jar'
  outjars 'build/libs/jmathtex.out.jar'

  // ProGuard cannot resolve MethodHandle.invokeExact
  dontwarn 'java.lang.invoke.MethodHandle'
  dontwarn 'java.awt.**'
  dontwarn 'org.jaxen.**'

  // Ensure reflection doesn't break
  dontobfuscate

  // A library has no entry point, so keep all class and member names intact
  keep 'public class com.whitemagicsoftware.tex.** { public *; }'

  keep name: 'org.apache.lucene.analysis.tokenattributes.KeywordAttributeImpl'
}
